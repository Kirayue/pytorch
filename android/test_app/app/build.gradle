apply plugin: 'com.android.application'

repositories {
    jcenter()
}

def props = new Properties()
file("../gradle.properties").withInputStream { props.load(it) }

def buildConfigProps = { k -> "\"${props.get(k)}\"" }

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    defaultConfig {
        applicationId "org.pytorch.testapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters ABI_FILTERS.split(",")
        }
        buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME'))
        buildConfigField ("String", "LOGCAT_TAG", "@string/app_name")
        addManifestPlaceholders([APP_NAME: "@string/app_name", MAIN_ACTIVITY: "org.pytorch.testapp.MainActivity"])
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
    flavorDimensions "model"
    productFlavors {
        mbq {
            dimension "model"
            applicationIdSuffix ".mbq"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbq\"")
        }
        mbqCamera {
            dimension "model"
            applicationIdSuffix ".mbqCamera"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_CAMERA", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera\"")
            buildConfigField ("int", "TU_TYPE", "0")
        }
        mbqCamera1 {
            dimension "model"
            applicationIdSuffix ".mbqCamera1"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_CAMERA1", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera1\"")
            buildConfigField ("int", "TU_TYPE", "1")
        }
        resnet18 {
            dimension "model"
            applicationIdSuffix ".resneti18"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_RESNET18'))
            addManifestPlaceholders([APP_NAME: "PyResNet18"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-resnet18\"")
        }


    }
    packagingOptions {
        pickFirst '**/libfbjni.so'
        doNotStrip '**.so'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation project(':pytorch_android')
    implementation project(':pytorch_android_torchvision')

    def camerax_version = "1.0.0-alpha05"
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation 'com.google.android.material:material:1.0.0-beta01'

}
